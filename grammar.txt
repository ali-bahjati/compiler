Prg -> Decl_list eof.
Decl_list -> Decl Decl_list | .
Decl -> Type_spc #decl_save_id id Decl'.
Decl' -> Var_decl_suf | Fun_decl_suf.
Var_decl_suf -> Var_decl'.
Var_decl' -> #decl_save_var ; | [ #decl_save_num num ] #decl_save_array ;.
Type_spc -> #decl_save_type int | #decl_save_type void.
Fun_decl_suf -> #scope_begin #func_reset_param ( Params ) #decl_save_func #scope_fun_begin Compnd_stmt_function #scope_func_end #scope_end #func_finalize. 
Params -> #func_inc_param #decl_save_type int #decl_save_id id Param' Param_list' | #decl_save_type void Params'.
Params' -> #decl_save_id id Param' Param_list' | .
Param_list -> Param Param_list'.
Param_list' -> , Param_list | .
Param -> Type_spc #decl_save_id id Param'.
Param' -> [ ] #decl_save_func_array | #decl_save_var .
Compnd_stmt_function -> { Decl_list Stmt_list }.
Compnd_stmt -> #scope_begin { Decl_list Stmt_list } #scope_end.
Stmt_list -> Stmt Stmt_list | .
Stmt -> Expr_stmt | Compnd_stmt | Sel_stmt | Iter_stmt | Ret_stmt | Switch_stmt.
Expr_stmt -> Expr ; | continue ; | break ; | ;.
Sel_stmt -> if ( Expr ) Stmt else Stmt.
Iter_stmt -> while ( Expr ) Stmt.
Ret_stmt -> return Ret_stmt'.
Ret_stmt' -> #return_check_void #func_finalize ; | Expr 	#return_expr #func_finalize ;.
Switch_stmt -> switch ( Expr ) { Case_stmts Def_stmt }.
Case_stmts -> Case_stmt Case_stmts | .
Case_stmt -> case num : Stmt_list.
Def_stmt -> default : Stmt_list | .
Expr_sec -> Term' Additive_expr' Simple_expr'.
Expr -> + Fact #adjust_sp Expr_sec | - Fact #adjust_sp Expr_sec | #gettemp_pid num Expr_sec | ( Expr ) Expr_sec | #pid id Expr'.
Expr' -> Call' Expr_sec | Var' Expr''.
Expr'' -> = Expr #assign_adjust_sp | Expr_sec.
Var -> id  Var'.
Var' -> [ Expr #find_address_aaray ] | .
Simple_expr' -> Relop Additive_expr #relop | .
Relop -> < | ==.
Additive_expr -> Term Additive_expr'.
Additive_expr' -> Addop Additive_expr #adjust_sp_add_or_minus | .
Addop -> + | -.
Term -> Signed_fact Term'.
Term' -> * Term #adjust_sp_mult | .
Signed_fact -> Fact #adjust_sp | + #push_1_to_ss Fact #adjust_sp_sign | - #push_minus_1_to_ss Fact #adjust_sp_sign.
Fact -> ( Expr ) | Conf_var_call | #gettemp_pid num.
Conf_var_call -> #pid id Conf_var_call'.
Conf_var_call' -> Var' #push_0_to_ss | Call'.
Call' -> ( Args ) #push_1_to_ss.
Args -> Arg_list | .
Arg_list -> Expr Arg_list'.
Arg_list' -> , Arg_list | .