Prg -> Decl_list #handle_main eof.
Decl_list -> Decl Decl_list | .
Decl -> Type_spc #decl_save_id id Decl'.
Decl' -> Var_decl_suf | Fun_decl_suf.
Var_decl_suf -> Var_decl'.
Var_decl' -> #decl_var_inc_sp #decl_save_var ; | [ #decl_save_num num ] #decl_arr_inc_sp #decl_save_arr ;.
Type_spc -> #decl_save_type int | #decl_save_type void.
Fun_decl_suf -> #scope_begin #scope_func_begin #func_reset_param ( Params ) #decl_func_inc_sp #decl_save_func Compnd_stmt_function #scope_end #func_finalize #scope_func_end.
Params -> #func_inc_param #decl_save_type int #decl_save_id id Param' Param_list' | #decl_save_type void Params'.
Params' -> #decl_save_id id Param' Param_list' | #decl_pop.
Param_list -> Param Param_list'.
Param_list' -> , Param_list | .
Param -> #func_inc_param Type_spc #decl_save_id id Param'.
Param' -> [ ] #decl_save_func_arr | #decl_save_var .
Compnd_stmt_function -> { Decl_list Stmt_list }.
Compnd_stmt -> #scope_begin { Decl_list Stmt_list } #scope_end.
Stmt_list -> Stmt Stmt_list | .
Stmt -> Expr_stmt | Compnd_stmt | Sel_stmt | Iter_stmt | Ret_stmt | Switch_stmt.
Expr_stmt -> Expr #pop_sem_st ; | continue ; | break ; | ;.
Sel_stmt -> if ( Expr ) #ifbeg Stmt #ifmid else Stmt #ifend.
Iter_stmt -> while ( Expr ) Stmt.
Ret_stmt -> return Ret_stmt'.
Ret_stmt' -> #func_finalize ; | Expr #return_expr #func_finalize ;.
Switch_stmt -> switch ( Expr ) { Case_stmts Def_stmt }.
Case_stmts -> Case_stmt Case_stmts | .
Case_stmt -> case num : Stmt_list.
Def_stmt -> default : Stmt_list | .
Expr_sec -> Term' Additive_expr' Simple_expr'.
Expr -> + Fact Expr_sec | - Fact #eneg Expr_sec | #pnum num Expr_sec | ( Expr ) Expr_sec | #pname id Expr'.
Expr' -> Call' Expr_sec | Var' Expr''.
Expr'' -> = Expr #assign #pval | #pval Expr_sec.
Var' -> #pvar [ Expr ] #parr | #pvar.
Simple_expr' -> #esym Relop Additive_expr #ecalc | .
Relop -> < | ==.
Additive_expr -> Term Additive_expr'.
Additive_expr' -> #esym Addop Additive_expr #ecalc | .
Addop -> + | -.
Term -> Signed_fact Term'.
Term' -> #esym * Term #ecalc | .
Signed_fact -> Fact | + Fact | - Fact #eneg.
Fact -> ( Expr ) | Conf_var_call | #pnum num.
Conf_var_call -> #pname id Conf_var_call'.
Conf_var_call' -> Var' #pval | Call'.
Call' -> #pfunc ( Args ) #func_call #func_ret.
Args -> Arg_list | .
Arg_list -> Expr #func_add_arg Arg_list'.
Arg_list' -> , Arg_list | .